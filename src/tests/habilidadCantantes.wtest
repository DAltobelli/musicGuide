import Musico.*
import luisAlberto.*
import guitarras.*
import estadoGrupal.*
import CobrarEInterpretar.*
import Banda.*

describe "test para saber si las habilidades de los cantantes varian correctamente"{

	const joaquin = new MusicoDeGrupo(tieneGrupo,new TocaSolo(100),new Larguero(300),20,5)
	const lucia = new VocalistaPopular(tieneGrupo, new PorCapacidad(500,5000), new Palabrero("familia"),70)
	const soledad = new VocalistaPopular(noTieneGrupo,new TocaSolo(6000),new Palabrero("amor"),55)
	const kike = new MusicoDeGrupo(tieneGrupo,new ExpectativaInflacionaria(4000,50,new Date(8,1,2018)),new Imparero(),60,20)
	
	/*Ahora tenemos bandas */
	const pimpisole = new Banda(#{soledad,joaquin,lucia},1982)
	const elEternoRetorno = new Banda(#{kike,luisAlberto},500)
	
	test "joaquin esta en pimpinela (habilidad=25)" {
		assert.equals(25,joaquin.habilidad())
	}
	
	test "joaquin se hace solista (habilidad=20)" {
		joaquin.abandonarGrupo()
		assert.equals(20,joaquin.habilidad())
	}
	
	test "lucia esta en pimpinela (habilidad=50)"{
		assert.equals(50,lucia.habilidad())
	}
	
	test "lucia se hace solista (habilidad=70)"{
		lucia.abandonarGrupo()
		assert.equals(70,lucia.habilidad())
	}
	test "alberto toca con la fader (habilidad=80)"{
		assert.equals(80,luisAlberto.habilidad())
	}
	test "alberto toca con la gibson sana (habilidad=100)"{
		luisAlberto.guitarraActual(gibson)
		assert.equals(100,luisAlberto.habilidad())
	}
	test "alberto toca con la gibson rota (habilidad=40)"{
		luisAlberto.guitarraActual(gibson)
		gibson.romperse()
		assert.equals(40,luisAlberto.habilidad())
	}
	test "la sole es vocalista y no esta en grupo, habilidad 55"{
		assert.equals(55,soledad.habilidad())
	}
	
	// Nuevos tests entrega 4
	
	test "La habilidad de pimpisole es (35+25+50)*1.1=121. La sole pasa a tener 35, por tocar en grupo"{
		assert.equals(121,pimpisole.habilidad())
	}
	test "La habilidad de elEternoRetorno es (80+80)*1.1=176"{
		assert.equals(176,elEternoRetorno.habilidad())
	}
}